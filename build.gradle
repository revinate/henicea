apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    springVersion = '1.2.4.RELEASE'
    cassandraVersion = '3.0.0'
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-actuator:${springVersion}"
    compile "com.datastax.cassandra:cassandra-driver-core:$cassandraVersion"
    compileOnly 'org.projectlombok:lombok:1.16.8'

    testCompile "org.springframework.boot:spring-boot-starter-test:${springVersion}"
    testCompile 'org.assertj:assertj-core:3.2.0'
}

group 'com.revinate'
archivesBaseName = 'henicea'
version = '1.1.1'

jar {
    baseName = 'henicea'
}

repositories {
    mavenCentral()
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.env.SONATYPE_USER, password: System.env.SONATYPE_PASS)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.env.SONATYPE_USER, password: System.env.SONATYPE_PASS)
            }

            pom.project {
                name 'Cassandra migration library'
                packaging 'jar'
                artifactId 'henicea'
                description 'Cassandra migrations for spring-boot apps'
                url 'https://github.com/revinate/henicea'

                scm {
                    connection 'scm:git:git://github.com/revinate/henicea.git'
                    developerConnection 'scm:git:ssh://git@github.com:revinate/henicea.git'
                    url 'https://github.com/revinate/henicea'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'revinate'
                        name 'Revinate, Inc'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
